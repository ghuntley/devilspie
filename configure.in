# Initial blurb
AC_PREREQ(2.52)
AC_INIT(devilspie, 0.10 , http://www.burtonini.com/)
AC_CONFIG_SRCDIR(src/devilspie.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(config.h)

# Honor aclocal flags
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

# Do the gettext/i18n stuff
GETTEXT_PACKAGE=devilspie
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [The name of the gettext package.])
AC_SUBST(GETTEXT_PACKAGE)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
GNOME_COMPILE_WARNINGS(yes)
AC_PROG_INTLTOOL([0.20])

# We really need gob2 to build.
GOB2_CHECK(2.0.3)
dnl TODO: say something if GOB2 is not found

PKG_CHECK_MODULES(TMP_WNCK, libwnck-1.0 >= 2.9.92,
		 wnck_new="yes", wnck_new="no")
if test "x$wnck_new" = "xyes"; then
        AC_DEFINE(NEED_TIMESTAMPS,1,[Define to 1 if timestamps are needed to wnck functions])
fi

# Check that we actually have the libraries required to
# build. Versioned dependancy on libwnck as essential API additions
# were made in 0.17.
dnl TODO: make libxml conditional and don't build docs if it isn't present
PKG_CHECK_MODULES(WNCK,
        gtk+-2.0
        libwnck-1.0 >= 0.17
        libxml-2.0)
AC_SUBST(WNCK_CFLAGS)
AC_SUBST(WNCK_LIBS)

# Now check if we have the set_fullscreen function. At some point we can support
# only wnck >= 2.8.0.1 and this test can go.
AC_CHECK_LIB(wnck-1, wnck_window_set_fullscreen,
            have_fullscreen=1, have_fullscreen=0)
AC_DEFINE_UNQUOTED(HAVE_FULLSCREEN, $have_fullscreen, [set_fullscreen is available])

# Check for the POSIX regexpr functions and headers
AC_CHECK_FUNCS([regcomp regexec regfree])
AC_CHECK_HEADERS([sys/types.h regex.h])

AC_CHECK_LIB(popt, poptStrippedArgv,, AC_MSG_ERROR([popt is needed to build]))

ALL_LINGUAS="de"
AM_GLIB_GNU_GETTEXT

AC_OUTPUT([
devilspie.spec
Makefile
src/Makefile
po/Makefile.in
])
